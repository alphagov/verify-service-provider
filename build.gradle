plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
}

project.ext {
    version_number = '0.4.0'
    openSamlVersion = '3.3.0'
    samlMetaDataBindingVersion = '58'
    samlExtensionsVersion = "$openSamlVersion-1.2a-33"
    dropwizardVersion = '1.2.0'
}

dependencies {
    compile(
        "io.dropwizard:dropwizard-core:$dropwizardVersion",
        'org.json:json:20171018',
        "org.opensaml:opensaml-core:$openSamlVersion",
        "uk.gov.ida:saml-metadata-bindings:$openSamlVersion-$samlMetaDataBindingVersion",
        "org.opensaml:opensaml-saml-impl:$openSamlVersion",
        "uk.gov.ida:saml-serializers:$openSamlVersion-24",
        "uk.gov.ida:saml-security:$openSamlVersion-46",
        "uk.gov.ida:ida-saml-extensions:$samlExtensionsVersion"
    )
    compile("commons-collections:commons-collections:3.2.2") { force = true }
    testCompile(
        'junit:junit:4.12',
        "io.dropwizard:dropwizard-testing:$dropwizardVersion",
        'org.mockito:mockito-core:2.11.0',
        "uk.gov.ida:saml-metadata-bindings-test:$openSamlVersion-$samlMetaDataBindingVersion",
        "uk.gov.ida:common-test-utils:2.0.0-31",
        "org.jsoup:jsoup:1.11.1",
    )
    testCompile('com.github.tomakehurst:wiremock:2.10.1'){ transitive = false }
}

sourceSets {
    test {
        resources {
            srcDir 'src/../configuration'
        }
    }

    acceptanceTest {
        java {
            srcDir 'src/acceptance-test/java'
        }
        resources {
            srcDir 'src/acceptance-test/resources'
            srcDir 'src/../configuration'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

version = "$version_number"

distributions {
    main {
        contents {
            from {
                ['README.md', 'RELEASE_NOTES.md', 'configuration/verify-service-provider.yml']
            }
        }
    }
}

jar {
    manifest {
        attributes(
                "Version": version
        )
    }
}

task testAcceptance(type: Test) {
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath += sourceSets.acceptanceTest.runtimeClasspath
}

run {
    args = ["server", "configuration/verify-service-provider.yml"]
}

mainClassName = 'uk.gov.ida.verifyserviceprovider.VerifyServiceProviderApplication'
