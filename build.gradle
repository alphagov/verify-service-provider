apply plugin: 'java'
apply plugin: 'application'

repositories {
    maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
}

dependencies {
    compile(
        'io.dropwizard:dropwizard-core:1.1.0',
        'org.json:json:20170516'
    )
    testCompile(
        'junit:junit:4.12',
        'io.dropwizard:dropwizard-testing:1.1.0',
        'org.mockito:mockito-core:2.7.22'
    )

}

// Allows us to use the private sun API for generating self signed certs in tests
compileTestJava {
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.compilerArgs << "-XDignore.symbol.file"
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integration-test/java'
        }
        resources {
            srcDir 'src/integration-test/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

distributions {
    main {
        contents {
            from {
                ['README.md', 'RELEASE_NOTES.md', 'configuration/verify-service-provider.yml']
            }
        }
    }
}

task testIntegration(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
}

run {
    args = ["server", "configuration/verify-service-provider.yml"]
}

mainClassName = 'uk.gov.ida.verifyserviceprovider.VerifyServiceProviderApplication'
